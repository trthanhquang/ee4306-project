//Vehicle 3
AddDifferentialDriveEntity    car3
	/Orientation:0    0   0
	/Position: 5.5   0    -6
	/Mass: 10
	AddLaserRangeFinderEntity    lrf_front3
		/Position:0  0.15  -0.25
		/ParentEntity:car3
		/Procedure_SensorNotify: laser_read3
	
	AddCompassEntity    compass3
		/Position:-0.1  0.15  -0.25
		/ParentEntity:car3
		/Procedure_SensorNotify: compass_read3

compassRaw3 = 0
leftAngle3 = 0
rightAngle3 = 0
leftDist3 =50000
rightDist3 = 50000


FlushScript  
Procedure car3Go(power)
{
	while(true)
	{
		call wallFollow3(0.5,power)
		wait 100
	}
}
End

Procedure wallFollow3(goalDist, basePower)
	Kh = 0.5 / 90.0;
	Kd = 0.3;
	angularMax = 0.07
	lateralMax = 0.05

	angularError = Util.ToInt (1000.0* leftAngle3)/1000.0
	angularOutput = Kh*angularError;

	if(angularOutput > angularMax)
		angularOutput = angularMax
	if(angularOutput < (-angularMax))
		angularOutput = -angularMax
	
	lateralError = Util.ToInt( 1000.0 *( goalDist - leftDist3))/1000.0;
	lateralOutput = Kd*lateralError

	if(lateralOutput > lateralMax)
		lateralOutput = lateralMax
	if(lateralOutput < (-lateralMax))
		lateralOutput = -lateralMax

	leftPower = basePower + angularOutput + lateralOutput
	rightPower = basePower - angularOutput - lateralOutput

	leftPower = Util.ToInt (1000.0* leftPower) / 1000.0
	rightPower = Util.ToInt (1000.0* rightPower) / 1000.0
	angularOutput = Util.ToInt (1000.0* angularOutput) / 1000.0
	lateralOutput = Util.ToInt (1000.0* lateralOutput) / 1000.0

//	print "(" + leftPower + " " + rightPower+ ") aErr: "+ angularError+", laErr: "+ lateralError + ", aOut: " + angularOutput + ",laOut: "+ lateralOutput
//	print "laErr: "+ lateralError + ", aErr: "+ angularError+ ", laOut: "+ lateralOutput + ", aOut: " + angularOutput
	car3.Go(leftPower, rightPower);
End
//--------------------------------------------------------------------------------------------------//
Procedure compass_read3
	compassRaw3 = value.RawMeasurement
End
//----------------------------------------------------------------------------------------------------
Procedure laser_read3
	left1 =value.DistanceMeasurements[360] / 1000.0
	left2 =value.DistanceMeasurements[330] / 1000.0
	right1 =value.DistanceMeasurements[0] / 1000.0
	right2 =value.DistanceMeasurements[30] /1000.0
	frontDist = value.DistanceMeasurements[180] / 1000.0

	call getAngleToWall(left1,left2)
	leftAngle3 = getAngleToWall
	
	call getAngleToWall(right1,right2)
	rightAngle3 = - getAngleToWall
	
	call getDistanceToWall(left1,left2)
	leftDist3 = getDistanceToWall + 0.2 * Math.Sin (leftAngle3* Math.PI /180.0)

	call getDistanceToWall(right1,right2)
	rightDist3 = getDistanceToWall - 0.2 * Math.Sin (rightAngle3 * Math.PI /180.0)
End

Procedure getAngleToWall(d1, d2)
	y = d2 * Math.Sin(Math.PI / 12);
	x = d1 - d2 * Math.Cos(Math.PI / 12);
	a = 90-(Math.Atan (y/x )/Math.PI *180.0);
	if (a>=90) 
		a = a-180;
	return a;
End

Procedure getDistanceToWall(d1,d2)
	temp = (d1 - d2 * Math.Cos(Math.PI /12))/(d2*Math.Sin( Math.PI /12));
	return d1/ Math.Sqrt (1 + Math.Pow (temp,2));
End